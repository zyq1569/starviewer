# File generated by kdevelop's qmake manager.
# ------------------------------------------
# Subdirectory related to the main project directory: ./src
# The goal is a subdirectory of the project

TEMPLATE = subdirs 

include(compilationtype.pri)


# addSubdirs(subdirs,deps): Adds directories to the project that depend on
# other directories
defineTest(addSubdirs) {
    for(subdirs, 1) {
        entries = $$files($$subdirs)
        for(entry, entries) {
            name = $$replace(entry, [/\\\\], _)
            SUBDIRS += $$name
            eval ($${name}.subdir = $$entry)
            for(dep, 2):eval ($${name}.depends += $$replace(dep, [/\\\\], _))
            export ($${name}.subdir)
            export ($${name}.depends)
        }
    }
    export (SUBDIRS)
}

# We add the subdirs to compile with their dependencies
addSubdirs(thirdparty)
addSubdirs(core)
addSubdirs(inputoutput, core)
addSubdirs(interface, core inputoutput)
addSubdirs(extensions, core inputoutput)
addSubdirs(main, interface extensions thirdparty)
addSubdirs(starviewersapwrapper)

# On linux 64bits, at the moment, breakpad is not supported, so we do not compile anything related.
include(detectlinux64.pri)
!linux_x86_64 {
  addSubdirs(crashreporter, main core)
}
